exprs      = exprsx

actuals    = <'[]'> | !'[]' <'['> exprsx <']'>
adverb     = '/' | '\\' | '/:' | '\\:' | '\'' | '\':'
advcore    = advcorex
<advcorex> = verb adverb
ladvcore   = ladvcorex
<ladvcorex>= lverb adverb
adverbed   = advin | !advin advpre | !(advin|advpre) advcorex
ladverbed  = ladvin | !ladvin ladvpre |(!ladvin|ladvpre) ladvcorex
<advin>    = lhs ws? advcorex ws? rhs
<ladvin>   = llhs ws? ladvcorex ws? lrhs
<advpre>   = advcorex ws? rhs
<ladvpre>  = ladvcorex ws? lrhs
aggs       = aggsx
<aggsx>    = lexprx ws? (<','> ws? lexprx)*
alhs       = dalhs | id | ktalhs | talhs | valhs
dalhs      = <'('> ws? arg ws? <'!'> ws? arg ws? <')'>
<ktalhs>   = ktarg
<talhs>    = targ
valhs      = !dalhs <'('> ws? (arg|hole) (ws? <';'> ws? (arg|hole))* ws? <')'>
           | <'('> ws? did (ws did)+ ws? <')'>
assign     = alhs ws? <':'> ws? rhs
lassign    = lid ws? <':'> ws? lrhs
at         = <loneat> | <'@'> ws? rhs | elhs ws? <'@'> ws? rhs
lat        = <loneat> | <'@'> ws? lrhs | lelhs ws? <'@'> ws? lrhs
bool       = #'[01]' <'b'>
bools      = #'[01][01]+' <'b'>
by         = <'by'> ws? aggsx
call       = target actuals
char       = <'"'> #'\\.|[^\"]' <'"'>
chars      = <'"'> #'(\\.|[^\"])(\\.|[^\"])+' <'"'>
col        = lid ws? <':'> ws? rhs
cols       = col ws? (<';'> ws? col)*
comment    = ws #'/.*'
dcols      = id (ws? <','> ws? id)*
<delete>   = <'delete'> ws (delrows|delcols)
delcols    = dcols ws from
delrows    = from ws? where?
dotn       = dotnx
<dotnx>    = id <'.'> (id|dotnx)
ldotn      = ldotnx
ldotnx     = lid <'.'> (lid|ldotnx)
dot        = !dotn (
             dotcore
           | dotcore ws? rhs
           | !(floatx|floatsx) elhs ws? dotcore ws? rhs
           | &floatsx lhs ws dotcore ws? rhs)
ldot       = !ldotn (
             dotcore
           | dotcore ws? lrhs
           | (!floatx|floatsx) lelhs ws? dotcore ws? lrhs
           | &floatsx llhs ws dotcore ws? lrhs)
<dotcore>  = !(floatx|floatsx) <'.'>
<dy>       = at | dot | dyop
<ldy>      = lat | ldot | ldyop
dyop       = lhs ws? op ws? rhs
ldyop      = llhs ws? lop ws? lrhs
empty      = '()'
expr       = exprx
<exprx>    = (delete|select|update)
           / dy
	   / dotn
           / nsvl
           / (call|juxt)
           / svl
(*           / <comment> *)
lexpr      = lexprx   (* limited expression: , is not join *)
<lexprx>   = (delete|select|update)
           / ldy
	   / ldotn
           / lnsvl
           / (call|ljuxt)
           / svl
(*           / <comment> *)
(* It can't be this simple.  Python doesn't have juxt *)
<exprsx>   = ws? (exprx|hole) (ws? <';'> ws? (exprx|hole))* ws?
	   (* | indent exprsx dedent *)
file       = <#'[ \t]*/.*\n'>* exprs
float      = !floatsx #'-?([0-9]*\.[0-9]+|[0-9]+\.[0-9]*)([eE][-+]?[0-9]+)?'
floats     = &(floatx | (longx ws)+ floatx) floatsx
<floatsx>  = #'-?(\.[0-9]+|[0-9]+(\.[0-9]*)?)([eE][-+]?[0-9]+)?(([ \n\r\t]+)+-?(\.[0-9]+|[0-9]+(\.[0-9]*)?)([eE][-+]?[0-9]+)?)+'
<floatx>   = #'-?(\.[0-9]+|[0-9]+\.[0-9]*)([eE][-+]?[0-9]+)?'
did        = idx | '_' (* destructuring id *)
<arg>      = darg | !varg did | ktarg | targ | varg
darg       = arg ws? <'!'> ws? arg
ktarg      = <'('> ws? <'['> ws? targs <']'> ws? targs ws? <')'>
targ       = <'('> ws? <'['> ws? <']'> ws? targsx ws? <')'>
targs      = targsx
<targsx>   = did ((ws? <';'> ws? did)* | (ws did)*)
varg       = <'('> ws? (arg|hole) (ws? <';'> ws? (arg|hole))* <')'>
           | did (ws did)+
formals    = <'['> ws? ((arg|hole) (ws? <';'> ws? (arg|hole))* ws?)? <']'>
from       = <'from'> ws? lexprx  (* lexprx? *)
hole       = ws?
id         = idx
lid        = lidx
<idx>      = #'[a-zA-Z][a-zA-Z0-9_]*'
<lidx>     = !keyword idx
juxt       = !(advin|delete|dy|floatsx|select|update)
             lhs ws? !dot rhs
           | &(floatsx(ws|'{'|'(')) lhs ws? !dot rhs
(* juxt       = floatsx ws? !dot rhs / !(advin|delete|dy|select!floatsx) lhs ws? !dot rhs *)
ljuxt      = !(advin|delete|dy|floatsx|select|update)
             llhs ws? !dot lrhs
           | &(floatsx(ws|'{'|'(')) llhs ws? !dot lrhs
keycols    = <'['> ws? col? ws? (<';'> ws? col)* ws? <']'>
keyword    = 'by'|'delete'|'from'|'select'|'update'|'where'
lambda     = <'{'> ws? formals? ws? exprs <'}'>
lhs        = lhsx
llhs       = llhsx
elhs       = advcore|lhsx|op (* at and dot support op on lhs *)
lelhs      = ladvcore|llhsx|lop
<lhsx>     = svl / (call | dotn | id | lit | parexpr)
<llhsx>    = call | ldotn | lid | !svl lit | parexpr | svl
<lit>      = bool | char | empty | float | lambda | long | symbol | table | vec
long       = !(bool|bools|longsx) longx
longs      = longsx
<longx>    = #'-?[0-9]+'
<longsx>   = #'-?[0-9]+([ \n\r\t]+-?[0-9]+)+'
monop      = !(nfloat|nlong) op ws? rhs
lmonop     = !(nfloat|nlong) lop ws? rhs
nfloat     = #'-([0-9]*\.[0-9]+|[0-9]+\.[0-9]*)([eE][-+]?[0-9]+)?'
nlong      = #'-[0-9]+'
<nsvl>     = adverbed | assign | id | lit | monop | op | parexpr
<lnsvl>    = ladverbed | lassign | lid | lit | lmonop | lop | parexpr
op         = '!' | '#' | '$' | '+' | '-' | '*' | '%' | ',' | '&' | '_' | '|' | '?' | relop
lop        = '!' | '#' | '$' | '+' | '-' | '*' | '%' | '&' | '_' | '|' | '?' | relop
parexpr    = <'('> exprx <')'>
<relop>    = '~' | (!'<=' '<') | '<=' | '>=' | (!'>=') '>' | '=' | '<>'
rhs        = exprx
lrhs       = lexprx
select     = <'select'> ws? aggs? ws? (by ws)? from ws? where?
<svl>      = bools | chars | floats | longs | symbols (* simple vector literal *)
symbol     = !symbolsx <'`'> #'[\.:a-zA-Z0-9_+\-*%/\\]*'
symbols    = symbolsx
<symbolsx> = #'`[\.:a-zA-Z0-9_+\-*%/\\]*(`[\.:a-zA-Z0-9_+\-*%/\\]*)+'
table      = <'('> ws? keycols ws? <';'>? ws? cols ws? <')'>
loneat     = <'@'>
lonedot    = <'.'>
target     = loneat|lonedot|lhsx|op
update     = <'update'> ws? aggs? ws? (by ws)? from ws? where?
vec        = <'('> ws? (exprx|hole) ws? (<';'> ws? (exprx|hole))+ ws? <')'>
verb       = advcore | at | call | dot | id | lambda | op | parexpr
lverb      = ladvcore | lat | call | ldot | lid | lambda | lop | parexpr
where      = <'where'> ws lexprx (ws? <','> ws? lexprx)*
<st>       = <#'[ \t]+'>
<ws>       = <#'([ \n\r\t]+(/.*)?)+'>
indent     = <'!!!'>
dedent     = <':::'>
